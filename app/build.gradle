import java.text.DateFormat
import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply from: "../artifacts.gradle"

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
def versionSuffix = "-" + getDateTimeVersion();
def versionCodeGen = getVersionCode();

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId "br.com.ande"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode versionCodeGen
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildTypes {
        debug {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".dev"
            versionNameSuffix "${versionSuffix}"
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.android.gms:play-services-maps:11.0.0'
    compile 'com.google.android.gms:play-services-location:11.0.0'
    compile 'com.android.support:support-vector-drawable:25.3.1'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.google.firebase:firebase-core:11.0.0'
    compile 'com.google.firebase:firebase-database:11.0.0'
    compile 'com.google.firebase:firebase-messaging:11.0.0'
    compile 'com.firebase:firebase-client-android:2.5.2+'
    compile 'com.airbnb.android:lottie:2.1.0'
    testCompile 'junit:junit:4.12'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
}

apply plugin: 'com.google.gms.google-services'

def getDateTimeVersion() {
    DateFormat df = new SimpleDateFormat("yyMMdd.HHmm");
    return df.format(new Date());
}

def getVersionCode() {
    DateFormat df = new SimpleDateFormat("yyyyMMddHH");
    return Integer.parseInt(df.format(new Date()));
}